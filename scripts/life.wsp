; adapted from rxi/fe's version
; https://github.com/rxi/fe/blob/master/scripts/life.fe

(set nth (func (n lst) (first (slice lst n n)) ))

(set rev (func (lst)
	(set res nil)
	(while lst
		(set res (cons (first lst) res))
		(set lst (rest lst))
	)
	res
))

(set map (func (f lst)
	(set res nil)
	(while lst
		(set res (cons (f (first lst)) res))
		(set lst (rest lst))
	)
	(rev res)
))

(set print-grid (func (grid)
	(map
		(func (row)
			(print (map (func (x) (if (= x 0) '- '#)) row))
		)
		grid
	)
))

(set get-cell (func (grid x y) (or (nth x (nth y grid)) 0) ))

(set next-cell (func (grid cell x y)
	(set n (+
		(get-cell grid (- x 1)  (- y 1))
		(get-cell grid (- x 1)  y      )
		(get-cell grid (- x 1)  (+ y 1))
		(get-cell grid x        (- y 1))
		(get-cell grid x        (+ y 1))
		(get-cell grid (+ x 1)  (- y 1))
		(get-cell grid (+ x 1)  y      )
		(get-cell grid (+ x 1)  (+ y 1))
	))

	(if
		(and (is cell 1) (or (= n 2) (= n 3))) 1
		(and (is cell 0) (= n 3)) 1
		0
	)
))

(set next-grid (func (grid)
	(set y -1)
	(map
		(func (row)
			(= y (+ y 1))
			(set x -1)
			(map
				(func (cell)
					(set x (+ x 1))
					(next-cell grid cell x y)
				)
				row
			)
		)
		grid
	)
))

(set life (func (n grid)
	(set i 1)
	(while (<= i n)
		(print ">> iteration" i)
		(print-grid grid)
		(print)
		(= grid (next-grid grid))
		(= i (+ i 1))
	)
))

; blinker in a 3x3 universe
(life 5 '(
	(0 1 0)
	(0 1 0)
	(0 1 0)
))
